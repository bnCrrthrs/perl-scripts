#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Std;
use HTTP::Tiny;
use Tie::File;

# future options: -p to print titles rather than editing the file ?
#+ handle options +#
our( $opt_h );
getopts('h');

if ($opt_h) {
  help_fn();
  exit 1;
}

my $csv = shift @ARGV || 0;
unless ( $csv =~ m/\.(c|t)sv$/i && -e $csv) { die "Please supply valid csv\n" };

my $seperator = lc($1) eq 'c' ? ',' : "\t";

# get original contents and standardise \r and \n linebreaks with \n
# this was necessary so Tie::File could read the lines properly
# also has the effect of removing empty lines
open ( my $FH_IN, '<', $csv ) or die "Could not read from $csv\n"; 
my $content = do {
  local $/; <$FH_IN>;
};
close ($FH_IN);

$content =~ s/(?:\h*\v)+/\n/mg;

open ( my $FH_OUT, '>', $csv ) or die "Could not write to $csv\n";
print $FH_OUT $content;
close ($FH_OUT);

# The way to change the record separator is an additional arg: recsep => "\r"
# Loop over lines and add titles
tie my @array, 'Tie::File', $csv, or die "\nCan't open file\n";

my $records = @array;

$array[0] .= "${seperator}Success${seperator}Redirects${seperator}Title";

for ( my $i = 1; $i < $records; $i++ ) {
#  next unless $array[$i] =~ m{(https?://[^\s]+)};
  next unless $array[$i] =~ m{(https?://.+)\h*$}m;
  my $url = $1;

  my $response = HTTP::Tiny->new->get( $url );

  unless ( $response->{success} ) {
    $array[$i] .= ${seperator} . wrap("FAIL: $response->{reason}");
    next;
  }

  my $phrase = wrap($response->{ reason });
  my $redirected = wrap($response->{redirects} ? $response->{url} : "none");
  my $title = wrap(response_title( $response->{content} ) || "none");
  $array[$i] .= "${seperator}${phrase}${seperator}${redirected}${seperator}${title}";

}

untie @array;

print "Finished looking for urls in $csv\n";

1;

sub wrap {
  return "\"$_[0]\"" ;
}

sub valid_url {
  return $_[0] =~ m{^"https?://}i;
};

sub response_title {
  if ( $_[0] =~ m{<title[^>]*>(.+)</title>}i ) {
    return $1;
  } else {
    return undef;
  }
}

sub help_fn {
  print "\nurl-titles-from-csv.pl\n", "======================\n";
  print "Takes a (single) csv or tsv as an argument and retrieves the web-page titles of urls contained in it.\n";
  print "It was written originally to be used with csv files generated by my exportHyperlinks.js indesign script\n";
  print "which is at https://github.com/bnCrrthrs/indesign-scripts. But it can be used with any csv or tsv file;\n";
  print "as long as the urls start with 'http(s?)://' and are be placed in the last column of the file.\n\n";
  print "Options:\n--------\n";
  print "-h) Prints this help menu and exits.\n";
  print "\n";
}
